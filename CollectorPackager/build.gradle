/**
 * Sapelli data collection platform: http://sapelli.org
 *
 * Copyright 2012-2016 University College London - ExCiteS group
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    repositories {
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        // Gradle plugin for creating fat/uber JARs with support for package relocation.
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.0'
        // Gradle Versions Plugin
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.14.0'
    }
}

apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'com.github.ben-manes.versions'

defaultTasks 'clean', 'install'

def artifactId = 'sapelli-collector-packager'

compileJava {
    options.encoding = 'UTF-8'
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

dependencies {

    // Sapelli Library:
    compile project(':Library')

    // Other dependencies:
    //  For command line interaction:
    compile 'commons-cli:commons-cli:1.4'

    // Add Logging tools
    compile "ch.qos.logback:logback-classic:1.1.3"
    compile "org.slf4j:log4j-over-slf4j:1.7.13"

    // Add JetBrains Annotations
    compile 'org.jetbrains:annotations:15.0'

    // Add project lombok
    compile 'org.projectlombok:lombok:1.16.16'

}

// Define Logger
configurations.all {
    exclude group: "org.slf4j", module: "slf4j-log4j12"
    exclude group: "log4j", module: "log4j"
}


shadowJar {
    // Name the file sapelli-collector-packager.jar
    baseName = artifactId
    classifier = 'with-dependencies'
    version = version
    mainClassName = 'uk.ac.ucl.excites.sapelli.packager.Packager'

    manifest {
        //noinspection GroovyAssignabilityCheck
        attributes 'Implementation-Title': 'Sapelli Packager',
                'Implementation-Version': version,
                'Built-By': System.getProperty('user.name'),
                'Built-JDK': System.getProperty('java.version'),
                'Created-By': 'Gradle'
    }
}
