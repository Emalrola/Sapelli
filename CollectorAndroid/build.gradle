apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'hu.supercluster.paperwork'

repositories {
    maven { url 'https://maven.fabric.io/public' }
    maven { url "https://jitpack.io" }
}

ext {
    // Read private/external fabric.properties file:
    fabric = new Properties();
    try
    {
        fabric.load(new FileInputStream(file('../../Keys/fabric.properties')));
    }
    catch(Exception e)
    {
        println('Using dummy fabric apiKey/Secret');
        fabric.setProperty('apiKey', '0000000000000000000000000000000000000000');
        fabric.setProperty('apiSecret', '0000000000000000000000000000000000000000000000000000000000000000');
    }

    // Update local fabric.properties file:
    ant.propertyfile(file: 'fabric.properties') {
        entry( key: "apiSecret", value: fabric['apiSecret'])
    }

    // Read private/external release signing key:
    signingKeyReleaseProperties = new Properties()
    signingKeyReleaseProperties.load(new FileInputStream(file('../../Keys/release.properties')))

    signingKeyDebugProperties = new Properties()
    signingKeyDebugProperties.load(new FileInputStream(file('../../Keys/debug.properties')))

    // We use the number of hours since 2012-01-01T00:00:00.000Z as the versionCode
    buildTimeBasedVersionCode = {
        def c = Calendar.instance;
        c.clear();
        c.timeZone = TimeZone.getTimeZone("UTC");
        c.set(2012, 0, 1);
        (int) ((System.currentTimeMillis() - c.timeInMillis) / 1000 / 60 / 60);
    }
}

// Generates paperwork.json asset file containing build information
// (which we read from in the BuildInfo class of the app):
paperwork {
    set = [
            buildTimeUTC: buildTime("yyyy-MM-dd'T'HH:mm:ss", "UTC"),
            gitSha      : gitSha(),
            gitTag      : gitTag(),
            gitInfo     : gitInfo(),
            gitBranch   : gitBranch(),
            userName    : System.getProperty("user.name") // OS user
    ]
}

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.1"

    signingConfigs {
        release {
            storeFile file("../../Keys/" + signingKeyReleaseProperties['keystore-path'])
            storePassword signingKeyReleaseProperties['keystore-passwd']
            keyAlias signingKeyReleaseProperties['key-alias']
            keyPassword signingKeyReleaseProperties['key-passwd']
        }
        debug {
            storeFile file("../../Keys/" + signingKeyDebugProperties['keystore-path'])
            storePassword signingKeyDebugProperties['keystore-passwd']
            keyAlias signingKeyDebugProperties['key-alias']
            keyPassword signingKeyDebugProperties['key-passwd']
        }
    }
    defaultConfig {
        applicationId "uk.ac.ucl.excites.sapelli.collector"
        minSdkVersion 10 // = GINGERBREAD_MR1 (as found on Samsung Galaxy Xcover(1)
        targetSdkVersion 22 // = LOLLIPOP_MR1 (for now we do not target 23 or higher, this is to avoid the run-time/revokable permissions of Android 6.0 and higher)

        versionName project.sapelliVersion // defined in root build.gradle file
        versionCode buildTimeBasedVersionCode() // the number of hours since 2012-01-01T00:00:00.000Z

        vectorDrawables.useSupportLibrary = true

        manifestPlaceholders = [fabricApiKey: fabric['apiKey']]
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            // Signing the app
            signingConfig signingConfigs.release
        }
        releaseTest {
            initWith(buildTypes.release)
            debuggable true
        }
        debug {
            debuggable true
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"

            // Signing the app
            signingConfig signingConfigs.debug
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    //testCompile 'junit:junit:4.12'
    // Sapelli Library:
    compile project(':Library')
    // Google:
    compile 'com.android.support:support-v4:24.1.1'
    compile 'com.android.support:appcompat-v7:24.1.1'
    // Other dependencies:
    compile 'com.github.michalis-vitos:aFileChooser:e359025948'
    compile 'com.caverock:androidsvg:1.2.2-beta-1'
    compile 'com.jpardogo.materialtabstrip:library:1.1.0'
    compile 'com.loopj.android:android-async-http:1.4.9'
    // Crashlytics Kit
    compile('com.crashlytics.sdk.android:crashlytics:2.6.1@aar') {
        transitive = true
    }
    // Paperwork
    compile 'hu.supercluster:paperwork:1.2.7'

    // Timber for logging:
    compile 'com.jakewharton.timber:timber:4.1.2'
    //  This lib is built using minSdkVersion=15, which we are overriding to the minSdkVersion
    //  of the app (currently 10) in AndroidManifest.xml, seemingly without problems. If we
    //  switch to a newer release of timber we must re-evaluate whether this is still needed
    //  and still does not cause problems.
}
